 
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {

    static Array powersOf2;
    static int qy2;

    /** Initializes the library. */
    function void init() {

        let powersOf2 = Array.new(16);
        let powersOf2[0] = 1;
        let powersOf2[1] = 2;
        let powersOf2[2] = 4;
        let powersOf2[3] = 8;
        let powersOf2[4] = 16;
        let powersOf2[5] = 32;
        let powersOf2[6] = 64;
        let powersOf2[7] = 128;
        let powersOf2[8] = 256;
        let powersOf2[9] = 512;
        let powersOf2[10] = 1024;
        let powersOf2[11] = 2048;
        let powersOf2[12] = 4096;
        let powersOf2[13] = 8192;
        let powersOf2[14] = 16384;
        let powersOf2[15] = 32767 + 1; // overflow, will become -32768. Compiler cannot handle just typing that because 32768 is not a valid positive integer 
        
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return (-x);
        }
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, i, currPower;

        if ((x = 0) | (y = 0)) {
            return 0;
        }
        if (x = 1) {
            return y;
        }
        if (y = 1) {
            return x;
        }
        if (x = -1) {
            return -y;
        }
        if (y = -1) {
            return -x;
        }

        let sum = 0;
        let shiftedX = x;
        let i = 0;        

        let currPower = 1; // 2^0

        while ((~(currPower > (y + 1)) | (y < 0)) & (i < 16)) {
            if ((y & currPower) = currPower) { // check i'th bit of y using bitwise and
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
            let currPower = currPower + currPower;
        }
        
        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q;

        let qy2 = 0;

        if (x = 0) {
            return 0;
        }
        if (y = 1) {
            return x;
        }
        if (y = -1) {
            return -x;
        }

        let q = Math._div(Math.abs(x), Math.abs(y));
        
        if ((x < 0) = (y < 0)) {
            return q;
        }
        return -q;
    }

     function int _div(int x, int y) {
        var int q;

        if ((y > x) | (y < 0)) { // check for overflow
            let qy2 = 0;
            return 0;
        }

        if (y < 16384) { // prevent infinite loop
            let q = Math._div(x, (y + y));
        }
        else {
            let q = 0;
        }

        if ((x - qy2) < y) {
            return (q + q);
        }
        let qy2 = qy2 + y;
        return (q + q + 1);
    }


    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y;
        var int j;
        var int potentialRoot;
        var int numBits;
        var int i;
        var int intermediateStep;

        if ((x = 0) | (x = 1)) {
            return x;
        }

        let y = 0;
        let numBits = 0;
        let i = 0;
        
        while (i < 15) {
            if (x > powersOf2[i]) {
                let numBits = numBits + 1;
                let i = i + 1;
            }
            else {
                let i = 15;
            }
        }

        let j = (numBits / 2);

        while (j > -1) {
            let potentialRoot = y + powersOf2[j];
            let intermediateStep = (potentialRoot * potentialRoot);
            if (~(intermediateStep > x) & (intermediateStep > 0)) {
                let y = potentialRoot;
            }

            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }
}
